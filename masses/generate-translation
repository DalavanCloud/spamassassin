#!/usr/bin/perl -w
#
# <@LICENSE>
# Copyright 2004 Apache Software Foundation
# 
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
# 
#     http://www.apache.org/licenses/LICENSE-2.0
# 
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
# </@LICENSE>

use strict;
use warnings;
use FindBin;
use Getopt::Std;

# Lingua::Translate has a bunch of requirements (see bottom of its perldoc)
use Lingua::Translate;

# %rules and %scores from tmp/rules.pl
use vars qw { $opt_h $opt_c $opt_e $opt_r %rules %scores };

sub usage {
  die "generate-translation language output_file

    -e STR   use STR as destination character set encoding (might not work)
    -c DIR   use DIR as rules directory
    -r STR   use STR as destination character set encoding (using recode)
    -h       print this help

  language should be a two letter language code from this list:

     zh: Chinese-simplified
     zt: Chinese-traditional
     nl: Dutch
     fr: French
     de: German
     el: Greek
     it: Italian
     ja: Japanese
     ko: Korean
     pt: Portuguese
     ru: Russian
     es: Spanish

  translation is displayed on standard output
  progress is displayed on standard error
";
}

getopts("hc:e:r:");
usage() if ($opt_h || @ARGV < 2);

my $dest = shift @ARGV;
my $output = shift @ARGV;
my $cffile = $opt_c || "$FindBin::Bin/../rules";
my $enc = $opt_e || "utf8";
my $recode = $opt_r || "UTF-8";

my $okay = '';
my $none = '';
my %lang_cache;

read_rules($cffile);
generate_translation();
print_translation();

sub read_rules {
  my ($cffile) = @_;

  # read rules data
  system("$FindBin::Bin/parse-rules-for-masses -d \"$cffile\"") and die;
  require "./tmp/rules.pl";
}

sub generate_translation {
  my $fish = Lingua::Translate->new(src => "en",
				    dest => $dest,
				    dest_enc => $enc)
      or die "No translation server available for en -> $dest";

  my $count = 0;
  for my $name (sort keys %rules) {
    my $lang_name = $name;
    my $lang_describe = '';
    if ($rules{$name}->{lang}) {
      print "skipping $name with lang $rules{$name}->{lang}\n";
    }
    elsif (defined $rules{$name}->{describe}) {
      # translate name if it appears in the description
      my $describe = $rules{$name}->{describe};
      if ($describe =~ /$name/) {
	eval {
	  $lang_name = $fish->translate($name); # dies or croaks on error
	};
	if ($@) {
	  $lang_name = '[A-Z]+[A-Z0-9_]+[A-Z0-9]';
	}
      }

      # translate description
      eval {
	if (defined $lang_cache{$describe}) {
	  $lang_describe = $lang_cache{$describe};
	}
	else {
	  # dies or croaks on error
	  $lang_describe = $fish->translate($describe);
	  $lang_cache{$describe} = $lang_describe;
	}
      };
      # didn't work
      if ($@) {
	$none .= "lang $dest describe $name\t" . $describe . "\n";
	print STDERR "none: $name\t$describe\n";
      }
      # worked
      else {
	$lang_describe =~ s/$lang_name/$name/;
	print "$lang_name $name\n" if $lang_name ne $name;
	$okay .= "# describe $name\t" . $describe . "\n";
	$okay .= "lang $dest describe $name\t" . $lang_describe . "\n\n";
	print STDERR "okay: $name $lang_describe\n";
      }
    }
    $count++;
    #last if $count > 10;
  }
}

sub print_translation {
  my $charset = $enc;
  if ($opt_r) {
    $charset = $recode;
  }
  $charset =~ s/utf-?8/utf-8/i;

  open(OUTPUT, "> $output");
  print OUTPUT <<'EOF';
#
# Please don't modify this file as your changes will be overwritten with
# the next update. Use @@LOCAL_RULES_DIR@@/local.cf instead.
# See 'perldoc Mail::SpamAssassin::Conf' for details.
#
# <@LICENSE>
# Copyright 2004 Apache Software Foundation
# 
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
# 
#     http://www.apache.org/licenses/LICENSE-2.0
# 
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
# </@LICENSE>
#
###########################################################################

# character set used in the following texts
EOF
  print OUTPUT <<EOF;
lang $dest report_charset $charset

# make me pretty
EOF
  open(MISC, "$FindBin::Bin/../rules/10_misc.cf");
  while (<MISC>) {
    if (/^(?:clear_report_template|report|clear_unsafe_report_template|unsafe_report)\b/) {
      print OUTPUT "lang $dest $_";
    }
  }

  print OUTPUT "\n# good translations\n\n";
  print OUTPUT "$okay\n";
  print OUTPUT "\n# unfinished translations\n\n";
  print OUTPUT "$none\n";
  system("/usr/bin/recode $enc..$recode $output") if $opt_r;
}
